include("Token.jl")
include("Errors.jl")

using JSON

const table = Dict("int"=>INT_NUMBER, "real"=>FLOAT_NUMBER, "char"=>CHAR, "texto"=>STRING, "boleano"=>BOOL)
const map_por_to_julia = Dict("int"=>Int64, "real"=>Float64, "boleano"=>Bool)
const map_julia_to_por = Dict(Int64=>"int", Float64=>"real", Bool=>"boleano")


iscolon(t::Token)   = t.id==PUNCTUATION && t.text==":"
iscoma(t::Token)    = t.id==PUNCTUATION && t.text==","
isperiod(t::Token)  = t.id==PUNCTUATION && t.text==";"
isassign(t::Token)  = t.id==ASSIGN && t.text==":="
isliteral(t::Token) = t.id==INT_NUMBER || t.id==FLOAT_NUMBER || t.id==CHAR || t.id==STRING || t.id==BOOL
isnumber(t::Token, env)  = (table[env[t.text].type] == INT_NUMBER) || (table[env[t.text].type] == FLOAT_NUMBER) || (table[env[t.text].type] == BOOL)

type_match(t::Token, lit::Token, env) = table[env[t.text].type] == lit.id
isop(t::Token) = t.id == OPERATOR || (t.text == ")" || t.text == "(")

mutable struct var_state
    name   :: String
    init   :: Bool
    type   :: String # can be: 'int', 'real', 'char', 'texto', 'boleano'
    value  :: String
    function var_state(name, init=false, type="", value="")
        new(name, init, type, value)
    end
end

type(env, t)  = env[t.text].type
value(env, t) = env[t.text].value
init(env, t)  = env[t.text].init

type!(env, t, v)  = env[t.text].type = v
value!(env, t, v) = env[t.text].value = v
init!(env, t, v)  = env[t.text].init = v


function syntactic_parse(tokens::Tokens)::Dict
    env = Dict()

    t = next!(tokens)
    if t.id == RESERVED_WORD && t.text == "programa"
        t = next!(tokens)
        if t.id == IDENTIFIER
            bloco!(tokens, env, ["fimprog"])

        else
            error("Parsing Error - Expecting name of program.",t)
        end
    else
        error("Parsing Error - Expecting PROGRAMA.",t)
    end

    t = next!(tokens)

    if t.id != EOF
        error("Program finished but file has content.",t)
    end

    println(json(env, 4))
    return env
end



function declare!(tokens::Tokens, env)
    t = next!(tokens)
    current_declared_vars::Array{var_state} = []

    while t.id == IDENTIFIER || iscoma(t)
        if t.id == IDENTIFIER
            push!(current_declared_vars, var_state(t.text))
        end

        t = next!(tokens)
    end

    if t.id == RESERVED_WORD || t.id == CFLUX
        error("Reserved words can't be used as variable names `$(t.text)`.", t)
    end


    if iscolon(t)
        t = next!(tokens)
        if t.id == TYPE
            map(x->x.type=t.text, current_declared_vars)
        else
            error("`$(t.text)` is not a correct TYPE.", t)
        end

        t = next!(tokens)
        if isperiod(t)
            # Returned with success
            for state in current_declared_vars
                env[state.name] = state
            end
            return

        else
            error("`$(t.text)` Not an end of line.", t)
        end
    else
        error("Not correct DECLARE stmt.", t)
    end
end



function bloco!(tokens::Tokens, env, expected_end)
    t = next!(tokens)

    if t.id==RESERVED_WORD && t.text=="declare"
        declare!(tokens, env)

    elseif t.id==RESERVED_WORD && (t.text=="leia" || t.text=="escreva")
        cmd_io!(tokens, env, t.text)

    elseif t.id==IDENTIFIER
        cmd_attr!(tokens, env)

    elseif t.id==CFLUX
        control_flux_parser!(tokens, env)
    end

    if !any(x->x==t.text, expected_end)
        bloco!(tokens, env, expected_end)
    end
end


function cmd_io!(tokens::Tokens, env, io)
    t = next!(tokens)
    if t.id==PUNCTUATION && t.text=="("
        t = next!(tokens)
        if t.id==IDENTIFIER
            if haskey(env, t.text)
                if io == "leia"
                    init!(env, t, true)
                else # escreva
                    println(json(env, 4))

                    if !init(env, t)
                        error("Trying to print uninitialized variable `$(t.text)`", t)
                    end
                end # io
            else
                error("Trying to read from undeclared variable `$(t.text)`.", t)
            end # env

        else # id
            error("Values inside `$io` command must be a variable, not `$(t.text)`.", t)
        end # id
        t = next!(tokens)
        if t.id!=PUNCTUATION && t.text==")"
            error("Missing closing parenthesis in `$io` stmt.", t)
        end # )

        t = next!(tokens)
        if !isperiod(t) error("Missing period.", t) end
    else # (
        error("Missing open parenthesis in `$io` stmt.", t)
    end # (
end


function cmd_attr!(tokens::Tokens, env)
    # var := literal | express√£o
    variable = current(tokens)

    if haskey(env, variable.text)
        t = next!(tokens)
        if isassign(t)
            t = next!(tokens)
            if isliteral(t) && !isop(next(tokens))
                if type_match(variable,t, env)
                    value!(env, variable, t.text)
                    init!(env, variable, true)
                else # not compatible types
                    error("Mismatching types between `$(variable.text)` and literal of type `$(t.id)`.", t)
                end #type_match
            else # should be expr
                a_value, a_type = par_expr!(tokens, env, variable)
                value!(env, variable, string(a_value))
                init!(env, variable, true)
            end # if is literal

            t = next!(tokens)
            if !isperiod(t) error("Missing semicolon.", t) end
        else # isn't assign symbol
            error("Assign symbol required.", t)
        end #isassing

    else #not has key
        error("Undefined variable `$(variable.text)`.", variable)
    end # haskey
end


function par_expr!(tokens::Tokens, env, expecting=nothing)
    expr = expr_to_text(tokens, env)

    expected_type = typeof(expecting)==Token ? type(env, expecting) : expecting

    if expr == ""
        roll_back(tokens)
        return "", expected_type
    end

    eval_value = nothing
    could_be_eval = false
    try
        eval_value = Meta.parse(expr)
        try
            eval_value = eval(eval_value)
            could_be_eval = true
        catch
            eval_value = expr
        end
    catch
        error("Failed to eval expression", current(tokens))
    end

    if typeof(eval_value) <: Number && abs(eval_value) == Inf error("Division by zero is not allowed", previous(tokens)) end

    atype = ""
    try
        if could_be_eval
            eval_value = map_por_to_julia[expected_type](eval_value)
            atype = map_julia_to_por[typeof(eval_value)]
        end
    catch
        atype = map_julia_to_por[typeof(eval_value)]
        error("Trying to cast an expression with incompatible types: `$expected_type` with `$atype`", expecting)
    end

    roll_back(tokens)
    return eval_value, atype
end


function expr_to_text(tokens::Tokens, env)
    t = current(tokens)
    expr = ""
    l = t.line

    while true
        # @show t
        if t.line != l
            roll_back(tokens)
            break
        end
        if isperiod(t) break end

        if t.id==CFLUX || t.id==TYPE || t.id==RESERVED_WORD
            error("Reserved words cant be used in expressions statements.", t)
        elseif t.id==ASSIGN
            error("Assign sinal in expression. Equal is `==`.", t)
        end

        val = t.text
        if t.id == IDENTIFIER
            if !haskey(env, t.text)
                error("Using undefined variable `$(t.text)`.", t)
            else # haskey
                if !isnumber(t, env)
                    error("Trying to use non numeric or logic values in expression.", t)
                end #isnumber
                if !init(env, t)
                    error("Trying to use non uninitialized variable `$(t.text)`.", t)
                end #init
                if value(env, t) != ""
                    val = value(env, t)
                end
            end #haskey
        end #if IDENTIFIER

        if val == "V" val = "true"
        elseif val == "F" val = "false"
        end

        expr *= val
        t = next!(tokens)

    end # while

    return expr
end

function control_flux_parser!(tokens, env)
    t = current(tokens)
    if t.text == "se"
        parse_se!(tokens, env)
    elseif t.text == "enquanto"
        parse_enquanto!(tokens, env)
    elseif t.text == "faca"
        parse_faca!(tokens, env)
    end
end


function parse_se!(tokens, env)
    next!(tokens)
    par_expr!(tokens, env, "boleano")
    t = next!(tokens)

    bloco!(tokens, env, ["fimse", "senao"])

    if current(tokens).text == "senao"
        bloco!(tokens, env, ["fimse"])
    end
end


function parse_enquanto!(tokens, env)
    next!(tokens)
    par_expr!(tokens, env, "boleano")
    t = next!(tokens)
    bloco!(tokens, env, ["fimenq"])
end


function parse_faca!(tokens, env)
    next!(tokens)
    bloco!(tokens, env, ["durante"])
    next!(tokens)
    par_expr!(tokens, env, "boleano")
end
